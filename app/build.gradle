apply plugin: 'com.android.application'

android {
    def conf = parent.ext;
    compileSdkVersion conf.androidCompileSdkVersion
    buildToolsVersion conf.androidBuildToolsVersion
    defaultConfig {
        applicationId conf.androidApplicationId
        minSdkVersion conf.androidMinSdkVersion
        targetSdkVersion conf.androidTargetSdkVersion
        versionCode conf.androidVersionCode
        versionName conf.androidVersionName
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        javaMaxHeapSize "2g"
        jumboMode = true
    }
    lintOptions {
        abortOnError false;
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":library")

    def libs = parent.ext.appLibs
    compile libs.supportV4
    provided libs.supportAnnotation

    compile 'com.jakewharton:butterknife:6.0.0'

    def test = parent.ext.appTestLibs
    testCompile test.junit
    testCompile test.robolectric

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3' // or 1.4-beta1
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3' // or 1.4-beta1
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3' // or 1.4-beta1
}
